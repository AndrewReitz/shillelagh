/*
 * Copyright 2014 Andrew Reitz
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath 'com.android.tools.build:gradle:1.0.0'
    classpath 'com.netflix.nebula:gradle-extra-configurations-plugin:1.12.3'
    classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.0'
    classpath 'org.jfrog.buildinfo:build-info-extractor-gradle:3.0.1'
  }
}

apply from: "gradle/dependencies.gradle"

ext {
  isSnapshot = !project.hasProperty('release')
  minSdkVersion = 15
  targetSdkVersion = 21
  versionCode = 1
}


allprojects {
  repositories {
    jcenter()
  }

  version = "1.0.0${isSnapshot ? '-SNAPSHOT' : ''}"
  group = "com.andrewreitz"

  apply plugin: "idea"
  apply plugin: "checkstyle"
  apply plugin: "optional-base"
  apply from: "$rootDir/gradle/credentials.gradle"

  checkstyle {
    configFile = new File(rootDir, "checkstyle.xml")
  }
}

task publishJavadoc(type: Exec) {
  dependsOn "javadoc"
  commandLine "bash", "-c",
'''
  REPO="git@github.com:pieces029/shillelagh.git"
  DIR=temp-clone

  # Delete any existing temporary website clone
  rm -rf $DIR

  # Clone the current repo into temp folder
  git clone $REPO $DIR

  # Move working directory into temp folder
  cd $DIR

  # Checkout and track the gh-pages branch
  git checkout -t origin/gh-pages

  # Delete everything
  rm -rf *

  # Copy everything from javadocs folder
  cp -r ../shillelagh/build/docs/javadoc/* .

  # Stage all files in git and create a commit
  git add .
  git add -u
  git commit -m "Website at \$(date)"

  # Push the new files up to GitHub
  git push origin gh-pages

  # Delete our temp folder
  cd ..
  rm -rf $DIR
'''
}
