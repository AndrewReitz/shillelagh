/*
 * Copyright ${year} Andrew Reitz
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
  repositories {
    jcenter()
    maven { url 'http://repo.springsource.org/plugins-release' }
  }
  dependencies {
    classpath 'com.android.tools.build:gradle:0.12.+'
    classpath 'org.springframework.build.gradle:propdeps-plugin:0.0.6'
    classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:0.5'
    classpath 'org.jfrog.buildinfo:build-info-extractor-gradle:2.2.5'
    classpath "org._10ne.gradle:rest-gradle-plugin:0.3.1"
  }
}

allprojects {
  repositories {
    jcenter()
  }

  version = "0.2.1${project.hasProperty('release') ? '' : '-SNAPSHOT'}"
  group = 'com.andrewreitz'

  ext {
    snapshot = !project.hasProperty('release')
    minSdkVersion = 14
    targetSdkVersion = 19
    versionCode = 1
  }

  apply plugin: 'idea'
  apply plugin: 'checkstyle'

  checkstyle {
    configFile = new File(rootDir, "checkstyle.xml")
  }
}

apply plugin: "org.10ne.rest"

task bintraySign(type: org._10ne.gradle.rest.RestTask) {
  description = "Sign the release build on bintray"
  httpMethod = 'post'
  uri = "https://api.bintray.com/gpg/pieces/maven/Shillelagh/versions/${version}"
  username = bintrayUser
  password = bintrayPassword
  requestBody = [passphrase: signingPassword]
  contentType = groovyx.net.http.ContentType.JSON
}
